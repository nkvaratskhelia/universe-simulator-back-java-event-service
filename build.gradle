plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.springframework.boot' version '3.0.2'
    id 'org.sonarqube' version '3.5.0.2730'
    id 'org.owasp.dependencycheck' version '8.0.2'
}

group = 'com.example.universe.simulator'
version = '1.0.0'
sourceCompatibility = '17'

idea {
    module {
        downloadJavadoc = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

def coverageExclusions = [
        '**/com/example/universe/simulator/eventservice/EventServiceApplication.*',
        '**/com/example/universe/simulator/eventservice/entities/Event.*'
]

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    reports {
        xml.required = true
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 1
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'universe-simulator'
        property 'sonar.login', System.getenv('US_SONARQUBE_TOKEN')
        property 'sonar.coverage.exclusions', coverageExclusions
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
}

bootBuildImage {
    imageName = "universesimulator/${project.name}:${System.getenv('GITHUB_RUN_NUMBER')}"
    publish = true
    docker {
        publishRegistry {
            username = System.getenv('US_DOCKER_REGISTRY_USER')
            password = System.getenv('US_DOCKER_REGISTRY_TOKEN')
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'GitHubPackages'
        url = 'https://maven.pkg.github.com/nkvaratskhelia/universe-simulator-back-java-common'
        credentials {
            username = System.getenv('US_GITHUB_PACKAGES_USER')
            password = System.getenv('US_GITHUB_PACKAGES_TOKEN')
        }
        metadataSources {
            gradleMetadata()
        }
    }
}

ext {
    set('commonVersion', '1.8.5')
    set('logstashLogbackEncoderVersion', '7.2')
    set('springBootAdminVersion', '3.0.0')
    set('springdocOpenapiVersion', '2.0.2')
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2022.0.1'
        mavenBom 'org.testcontainers:testcontainers-bom:1.17.6'
    }
}

dependencies {
    implementation "com.example.universe.simulator:common:${commonVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
    // needed for liquibase
    runtimeOnly 'org.springframework.boot:spring-boot-starter-data-jdbc'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-consul-config'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
    // needed for spring cloud load balancer
    runtimeOnly 'com.github.ben-manes.caffeine:caffeine'
    runtimeOnly 'org.liquibase:liquibase-core'
    runtimeOnly "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"
    // needed to prevent netty dns error on aarch_64 (mac M1)
    runtimeOnly "io.netty:netty-resolver-dns-native-macos:${dependencyManagement.importedProperties['netty.version']}:osx-aarch_64"
    // needed for liquibase
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'
    runtimeOnly "de.codecentric:spring-boot-admin-starter-client:${springBootAdminVersion}"
    runtimeOnly "org.springdoc:springdoc-openapi-starter-webflux-ui:${springdocOpenapiVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:rabbitmq'
    testImplementation 'io.projectreactor:reactor-test'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}
