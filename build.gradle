plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.4'
    id 'org.sonarqube' version '3.3'
}

group = 'com.example.universe.simulator'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

idea {
    module {
        downloadJavadoc = true
    }
}

test {
    useJUnitPlatform()
}

jacocoTestCoverageVerification {
    afterEvaluate {
        jacocoExclusions(classDirectories)
    }

    violationRules {
        rule {
            limit {
                minimum = 1
            }
        }
    }
}

private jacocoExclusions(classDirectories) {
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                'com/example/universe/simulator/eventservice/EventServiceApplication.class'
        ])
    }))
}

jacocoTestReport {
    afterEvaluate {
        jacocoExclusions(classDirectories)
    }
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', "https://sonarcloud.io"
        property 'sonar.organization', "universe-simulator"
        property 'sonar.login', System.getenv('US_SONARQUBE_TOKEN')
    }
}

bootBuildImage {
    imageName = "universesimulator/${project.name}"
    publish = true
    docker {
        publishRegistry {
            username = System.getenv('US_DOCKER_REGISTRY_USER')
            password = System.getenv('US_DOCKER_REGISTRY_PASSWORD')
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'GitHubPackages'
        url = System.getenv('US_GITHUB_PACKAGES_URL')
        credentials {
            username = System.getenv('US_GITHUB_PACKAGES_USER')
            password = System.getenv('US_GITHUB_PACKAGES_TOKEN')
        }
        metadataSources {
            gradleMetadata()
        }
    }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.3'
    }
}

dependencies {
    implementation 'com.example.universe.simulator:common:0.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-sleuth'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-vault-config'
    runtimeOnly 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}
